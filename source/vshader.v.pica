; Uniforms
.fvec projection[4]
.fvec modelView[4]
.fvec textureOffset
.fvec textureScale

; Constants
.constf myconst(0.0, 1.0, -1.0, 0.1)
.alias  zeros myconst.xxxx ; Vector full of zeros
.alias  ones  myconst.yyyy ; Vector full of ones

; Outputs
.out outpos position
.out outclr color
.out outtex texcoord0

; Inputs (defined as aliases for convenience)
.alias inpos v0
.alias inclr v1
.alias intex v2

.proc main
	; Force the w component of inpos to be 1.0
	mov r0.xyz, inpos
	mov r0.w,   ones

	; r1 = modelView * inpos
	dp4 r1.x, modelView[0], r0
	dp4 r1.y, modelView[1], r0
	dp4 r1.z, modelView[2], r0
	dp4 r1.w, modelView[3], r0

	; outpos = projectionMatrix * r1
	dp4 outpos.x, projection[0], r1
	dp4 outpos.y, projection[1], r1
	dp4 outpos.z, projection[2], r1
	dp4 outpos.w, projection[3], r1

	; outclr = inclr
	mov outclr, inclr

	; outtex = (intex*textureScale)+textureOffset
	; technically outtex = textureOffset+(textureScale*intex)
	mul r0, textureScale, intex
	add outtex, textureOffset, r0

	; We're finished
	end
.end
